plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.21'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
android {

    boolean doSign = keystorePropertiesFile.exists()

    if (doSign) {
        def keystoreProperties = new Properties()

        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            config {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
    }

    compileSdk 34
    defaultConfig {
        applicationId "com.utazukin.ichaival"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 45
        versionName "1.9.2"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            if (doSign) {
                signingConfig signingConfigs.config
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            pickFirsts += ['META-INF/atomicfu.kotlin_module']
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    namespace 'com.utazukin.ichaival'
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.5'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0"
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'com.github.seven332.Image:singlelib:a7b102262a'
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation platform('androidx.compose:compose-bom:2024.11.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.paging:paging-runtime-ktx:3.3.4"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.8.7"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.fragment:fragment-ktx:1.8.5"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "androidx.room:room-paging:$room_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "com.drakeet.drawer:drawer:1.0.3"
}
